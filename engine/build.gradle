apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'net.researchgate.release'

android {
    compileSdkVersion useSdk.toInteger()

    defaultConfig {
        minSdkVersion minSdk.toInteger()
        targetSdkVersion useSdk.toInteger()
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14 -frtti -fexceptions"
            }
        }
//        ndk {
//            abiFilters "x86_64"
//        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'USER_AGENT', '\"MXMapEngine\"'
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation group: 'io.reactivex.rxjava2', name: 'rxjava', version: rxJavaVersion
    implementation group: 'io.reactivex.rxjava2', name: 'rxandroid', version: rxJavaVersion
    implementation group: 'io.reactivex.rxjava2', name: 'rxkotlin', version: rxKotlinVersion

    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlinVersion

    implementation group: 'com.google.code.gson', name: 'gson', version: gsonVersion
    implementation project(':gestures')
    implementation project(':geojson')
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: okHttpVersion

    compileOnly group: 'com.google.auto.value', name: 'auto-value-annotations', version: autoValueVersion

    annotationProcessor group: 'com.ryanharter.auto.value', name: 'auto-value-parcel', version: autoValueParcelVersion
    annotationProcessor group: 'com.google.auto.value', name: 'auto-value-annotations', version: autoValueVersion

    compileOnly group: 'com.google.android.gms', name: 'play-services-location', version: gmsLocationVersion

    implementation 'android.arch.lifecycle:livedata:1.1.0'

    implementation group: 'com.android.support', name: 'appcompat-v7', version: androidSupportVersion
    implementation group: 'com.android.support', name: 'support-annotations', version: androidSupportVersion
    implementation group: 'com.android.support', name: 'support-fragment', version: androidSupportVersion
    implementation group: 'com.android.support', name: 'design', version: androidSupportVersion
    implementation group: 'com.android.support', name: 'recyclerview-v7', version: androidSupportVersion

    testImplementation 'junit:junit:4.12'
    androidTestImplementation group: 'com.android.support.test', name: 'runner', version: testRunnerVersion
    androidTestImplementation group: 'com.android.support.test.espresso', name: 'espresso-core', version: espressoVersion
}

apply from: rootProject.file('gradle/maven-publish-android.gradle')

bintray {
    user = bintray_user
    key = bintray_key
    dryRun = false
    publish = true
    publications = ['maven']
    pkg {
        userOrg = 'madrona'
        repo = 'maven'
        name = 'mxmapengine'
        version {
            name = project.version //Bintray logical version name
        }
        licenses = ['Apache-2.0', 'Boost-1.0', 'MIT']
        websiteUrl = 'https://github.com/manimaul/MXMapEngine'
        issueTrackerUrl = 'https://github.com/manimaul/MXMapEngine/issues'
        vcsUrl = 'https://github.com/manimaul/MXMapEngine.git '
        desc = 'Custom map rendering engine for Android'
        labels = ['android', 'mx mariner', 'map']
    }
}

afterReleaseBuild.dependsOn ':engine:bintrayUpload'
